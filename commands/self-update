#!/usr/bin/env zsh

###
# Print usage information
###
function _zulu_self-update_usage() {
  echo $(color yellow "Usage:")
  echo "  zulu self-update [options]"
  echo
  echo $(color yellow "Options:")
  echo "  -c, --check       Check if an update is available"
  echo "  -h, --help        Output this help text and exit"
}

###
# Update the zulu core
###
function _zulu_self-update_core() {
  local old="$(pwd)"

  cd $core
  git fetch origin && git rebase origin
  cd $old
}

function _zulu_self-update_check_for_update() {
  local old="$(pwd)"
  cd "$base/core"

  if command git rev-parse --abbrev-ref @'{u}' &>/dev/null; then
    count="$(command git rev-list --left-right --count HEAD...@'{u}' 2>/dev/null)"

    down="$count[(w)2]"

    if [[ $down > 0 ]]; then
      echo "$(color green 'New zulu version available') Run zulu self-update to upgrade"
      cd $old
      return
    fi
  fi

  echo $(color green 'No update available')
  cd $old
  return 1
}

###
# Update the zulu core
###
function _zulu_self-update() {
  local help base core check

  base=${ZULU_DIR:-"${ZDOTDIR:-$HOME}/.zulu"}
  core="${base}/core"

  # Parse options
  zparseopts -D h=help -help=help c=check -check=check

  # Output help and return if requested
  if [[ $help ]]; then
    _zulu_self-update_usage
    return
  fi

  if [[ $check ]]; then
    _zulu_self-update_check_for_update
    return
  fi

  if _zulu_self-update_check_for_update > /dev/null; then
    _zulu_spinner_start "Updating zulu core..."
    out=$(_zulu_self-update_core 2>&1)

    if [ $? -eq 0 ]; then
      _zulu_spinner_stop
      echo "$(color green '✔') Zulu core updated        "
    else
      _zulu_spinner_stop
      echo "$(color red '✘') Error updating zulu core        "
      echo "$out"

      return 1
    fi

    _zulu_load_commands

    return
  fi

  echo $(color green 'No update available')
}
