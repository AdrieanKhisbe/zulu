#!/usr/bin/env zsh

function _zulu_link() {
  local package json dir file link base index


  base="${ZULU_DIR:-${ZDOTDIR:-"$HOME/.config/zulu"}}"
  index="$base/.index"

  package="$1"

  # Check if the package is already installed
  root="$base/packages/$package"
  if [[ ! -d "$root" ]]; then
    echo $(color red "Package '$package' is not installed")
    return 1
  fi

  _zulu_spinner_start "Linking $package..."

  # Get the JSON from the index
  json=$(cat "$index/$package")

  # Loop through the 'bin' and 'share' objects
  dirs=('bin' 'init' 'share')
  for dir in $dirs[@]; do
    # Convert the bin/share object into an associative array
    typeset -A files=($(echo $(jsonval $json $dir) | tr "," "\n" | tr ":" " "))

    # Loop through each of the values in the array, the key is a file within
    # the package, the value is the name of a symlink to create in the directory
    for file link ("${(@kv)files}") ln -s "$root/$file" "$base/$dir/$link"

    # Make sure that commands to be included in bin are executable
    if [[ "$dir" = "bin" ]]; then
      for file link ("${(@kv)files}") chmod u+x "$root/$file"
    fi

    if [[ "$dir" = "init" ]]; then
      for file link ("${(@kv)files}") source "$base/init/$link"
    fi
  done

  _zulu_spinner_stop
  echo "$(color green 'âœ”') Finished linking $package        "
}
